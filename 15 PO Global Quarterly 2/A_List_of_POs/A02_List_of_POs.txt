COM ***********************************************************
COM  SCRIPT NAME:  A02_List_of_POs
COM  SCRIPT DESC:  Run script to extract list of POs
COM  CREATED DATE: 2/15/2021
COM  CREATED BY:   Marion Pacuri
COM  CALLED BY:    A00_Master    
COM  FILES IN:     S_EKKO_EKPO.FIL
COM                S_LFA1.FIL
COM                S_DD07T_ESTAK.FIL
COM                S_T161T.FIL
COM                S_T023T.FIL
COM                S_T052U.FIL
COM                S_T163I.FIL
COM                S_T16FT.FIL
COM                S_T16FH.FIL
COM                S_T161U.FIL
COM                S_T016T.FIL
COM                S_T077Y.FIL
COM                S_TCURR.FIL
COM                S_TCURF.FIL
COM                S_T001.FIL
COM                S_T163Y.FIL
COM                GAIMS_UserId1 (linked table in Robots)
COM  FILES OUT:    R1_PODetails.FIL
COM ***********************************************************


COM Set folder
SET FOLDER /Tables/List_of_POs

COM *****************************************************
COM Convert amounts to USD based on run dates exchange rate
COM *****************************************************

OPEN S_TCURR
EXTRACT FIELDS ALL TO T_EXCHANGERATE1 IF TCURR_KURST = "M"

COM Join the 2 currency tables - Validity dates and amounts
OPEN T_EXCHANGERATE1
OPEN S_TCURF SECONDARY
JOIN PKEY TCURR_KURST TCURR_FCURR TCURR_TCURR FIELDS TCURR_KURST TCURR_FCURR TCURR_TCURR TCURR_GDATU TCURR_UKURS SKEY TCURF_KURST TCURF_FCURR TCURF_TCURR WITH TCURF_FFACT TCURF_TFACT TO T_EXCHANGERATE2 OPEN PRESORT SECSORT

COM Convert the date into a readable format
OPEN T_EXCHANGERATE2
DELETE FIELD CUR_FROMDATE OK
DEFINE FIELD CUR_FROMDATE COMPUTED 

CTOD(SUBSTRING(STRING(919010101,9),2,8)) IF VALUE(TCURR_GDATU,0) >= 80989899
CTOD(SUBSTRING(STRING(999999999-VALUE(TCURR_GDATU,0),9),2,8))

COM Sort from the most recent to the oldest amount
SORT ON TCURR_FCURR TCURR_TCURR CUR_FROMDATE D to T_EXCHANGERATE3 IF SUBSTRING(TCURR_TCURR,1,3) = "USD" AND SUBSTRING(TCURR_TCURR,1,5) <> "USDGY"

COM Summarize to a single currency conversion amount 
OPEN T_EXCHANGERATE3
SUMMARIZE ON TCURR_FCURR TCURR_TCURR OTHER TCURF_FFACT TCURF_TFACT TCURR_UKURS CUR_FROMDATE TO "T_EXCHANGERATE4.FIL" OPEN  ISOLOCALE root

COM Join the non USD currency entrys with the exchange rate  
OPEN S_EKKO_EKPO
OPEN T_EXCHANGERATE4 SECONDARY
JOIN PKEY EKKO_WAERS FIELDS ALL SKEY TCURR_FCURR WITH TCURR_UKURS TCURR_FCURR CUR_FROMDATE TCURF_FFACT TCURF_TFACT  TO "T_PO_1a" OPEN PRIMARY PRESORT SECSORT 

COM Calculate the USD version
DELETE FIELD Converted_Amount OK
DEFINE FIELD Converted_Amount COMPUTED 

DEC(EKPO_NETWR,2)                                                  IF EKKO_WAERS = "USD"
0.00                                                               IF TCURF_FFACT = 0
DEC((TCURR_UKURS /  TCURF_FFACT) * EKPO_NETWR,2)                   IF TCURR_UKURS > 0 AND EKKO_WAERS <> "USD"
DEC(EKPO_NETWR / (TCURF_FFACT * TCURF_TFACT * ABS(TCURR_UKURS)),2) IF TCURR_UKURS <= 0 AND EKKO_WAERS <> "USD"
0.00

EXTRACT ALL FIELDS TO T_PO_1b OPEN 

DELETE FORMAT T_EXCHANGERATE3 OK
DELETE FORMAT T_EXCHANGERATE4 OK
DELETE FIELD TCURR_UKURS OK
DELETE FIELD TCURR_FCURR OK
DELETE FIELD CUR_FROMDATE OK
DELETE FIELD TCURF_FFACT OK
DELETE FIELD TCURF_TFACT OK

CLOSE PRIMARY SECONDARY

COM *****************************************************
COM Convert amounts to USD based on PO dates exchange rate
COM *****************************************************

OPEN T_EXCHANGERATE2

COM Sort from the most recent to the oldest amount
SORT ON TCURR_FCURR TCURR_TCURR CUR_FROMDATE D to T_EXCHANGERATE3 IF SUBSTRING(TCURR_TCURR,1,3) = "USD" AND SUBSTRING(TCURR_TCURR,1,5) <> "USDGY"

OPEN T_EXCHANGERATE3
DELETE FIELD CUR_ENDDATE OK 
DEFINE FIELD CUR_ENDDATE COMPUTED 

RECOFFSET(CUR_FROMDATE,-1) IF RECOFFSET(TCURR_FCURR,-1) = TCURR_FCURR AND RECOFFSET(TCURR_TCURR,-1) = TCURR_TCURR
CUR_FROMDATE

EXTRACT ALL FIELDS TO T_EXCHANGERATE4 IF BETWEEN(CUR_FROMDATE,v_dateLow,v_dateHigh)

OPEN S_EKKO_EKPO
SUMMARIZE ON EKKO_EBELN EKKO_WAERS EKKO_AEDAT TO T_SUMM_1 PRESORT OPEN

OPEN T_EXCHANGERATE4 SECONDARY
JOIN PKEY EKKO_WAERS FIELDS EKKO_EBELN EKKO_WAERS EKKO_AEDAT SKEY TCURR_FCURR WITH TCURR_UKURS TCURR_FCURR CUR_FROMDATE CUR_ENDDATE TCURF_FFACT TCURF_TFACT  TO "T_EXCHANGERATE5" OPEN MANY IF BETWEEN(EKKO_AEDAT,T_EXCHANGERATE4.CUR_FROMDATE,T_EXCHANGERATE4.CUR_ENDDATE) PRESORT SECSORT 
CLOSE PRIM SECON

OPEN T_EXCHANGERATE5
SORT ON EKKO_EBELN CUR_FROMDATE D TO T_SORT OPEN
SUMMARIZE ON EKKO_EBELN OTHER ALL TO T_EXCHANGERATE6 OPEN

COM Join the non USD currency entrys with the exchange rate  
OPEN T_PO_1b
OPEN T_EXCHANGERATE6 SECONDARY
JOIN PKEY EKKO_EBELN EKKO_WAERS FIELDS ALL SKEY EKKO_EBELN TCURR_FCURR WITH TCURR_UKURS TCURR_FCURR CUR_FROMDATE TCURF_FFACT TCURF_TFACT  TO "T_PO_1c" OPEN PRIMARY PRESORT SECSORT 

COM Calculate the USD version
DELETE FIELD Converted_Amount_PODate OK
DEFINE FIELD Converted_Amount_PODate COMPUTED 

DEC(EKPO_NETWR,2)                                                  IF EKKO_WAERS = "USD"
0.00                                                               IF TCURF_FFACT = 0
DEC((TCURR_UKURS /  TCURF_FFACT) * EKPO_NETWR,2)                   IF TCURR_UKURS > 0 AND EKKO_WAERS <> "USD"
DEC(EKPO_NETWR / (TCURF_FFACT * TCURF_TFACT * ABS(TCURR_UKURS)),2) IF TCURR_UKURS <= 0 AND EKKO_WAERS <> "USD"
0.00

EXTRACT ALL FIELDS TO T_PO_1

DELETE FORMAT T_EXCHANGERATE1 OK
DELETE FORMAT T_EXCHANGERATE2 OK
DELETE FORMAT T_EXCHANGERATE3 OK
DELETE FORMAT T_EXCHANGERATE4 OK
DELETE FORMAT T_EXCHANGERATE5 OK
DELETE FORMAT T_EXCHANGERATE6 OK
DELETE FORMAT T_PO_1a OK
DELETE FORMAT T_PO_1b OK
DELETE FORMAT T_PO_1c OK

CLOSE PRIMARY SECONDARY

COM *****************************************************
COM Index on reference text tables to get description
COM *****************************************************

COM Status of purchasing document
OPEN S_DD07T_ESTAK
INDEX ON DD07T_DOMVALUE_L TO STATUS_INX

COM Texts for Purchasing Document Types
OPEN S_T161T
INDEX ON ALL(T161T_BSART)+ALL(T161T_BSTYP) TO DOCTYPE_INX

COM Material Group Descriptions
OPEN S_T023T
INDEX ON T023T_MATKL TO MATGRP_INX

COM Own Explanations for Terms of Payment
OPEN S_T052U
INDEX ON T052U_ZTERM TO PAYTERM_INX

COM Account Group Names
OPEN S_T077Y
INDEX ON T077Y_KTOKK TO ACCTGRP_INX

COM Vendor Master Data - Company Code for Payment Terms
OPEN S_LFA1_LFB1
INDEX ON LFA1_LIFNR + LFB1_BUKRS TO VENDOR_PAYTERM_INX

COM Text for Company Code Names
OPEN S_T001
INDEX ON T001_BUKRS TO COCODE_INX

COM Text for Plant Code Names
OPEN S_T001W
INDEX ON T001W_WERKS TO PLANT_INX

COM Texts for Item Categories
OPEN S_T163Y
INDEX ON T163Y_PSTYP TO ITEMCAT_INX

COM GAIMS User IDs
OPEN GAIMS_UserId1
INDEX ON SUB(ALL(USER_ID) 1 10) TO GAIMS_INX

COM Direct Indirect Material Group 
OPEN S_DIRECT_INDIRECT
INDEX ON ALL(Mat_Group) TO INDEX_DIRECT_INDIRECT

COM Join PO table with VMD to get Vendor Names
OPEN T_PO_1
OPEN S_LFA1_LFB1 SECONDARY
JOIN PKEY EKKO_LIFNR EKKO_BUKRS FIELDS ALL SKEY LFA1_LIFNR LFB1_BUKRS WITH LFA1_NAME1 LFA1_NAME2 LFA1_NAME3 LFA1_KTOKK LFB1_ZTERM TO T_PO_2 PRIMARY PRESORT SECSORT 
CLOSE PRIMARY SECONDARY
DELETE FORMAT T_PO_1 OK

COM Exclude Intercompany Vendor Transactions 
OPEN T_PO_2
EXTRACT ALL FIELDS TO T_PO_3 IF NOT MATCH(LFA1_KTOKK,"PLTS","0007","INTP","KONS","EXPA","PLTS","VIBS","")
DELETE FORMAT T_PO_2 OK

COM Add field for PO USD Summary
OPEN T_PO_3
SUMMARIZE ON EKKO_EBELN SUBTOTAL Converted_Amount TO T_SUMM PRESORT OPEN

OPEN T_PO_3
OPEN T_SUMM SECONDARY
JOIN PKEY EKKO_EBELN FIELDS ALL SKEY EKKO_EBELN WITH Converted_Amount AS "PO_USD_Summ" TO T_PO_4 PRIMARY PRESORT SECSORT
CLOSE PRIMARY SECONDARY
DELETE FORMAT T_PO_3 OK

COM Define relation with reference text tables
OPEN T_PO_4
DEFINE RELATION EKKO_LIFNR + EKKO_BUKRS WITH S_LFA1_LFB1 INDEX VENDOR_PAYTERM_INX
DEFINE RELATION S_LFA1_LFB1.LFB1_ZTERM WITH S_T052U INDEX PAYTERM_INX AS VMD_PAYTERM
DEFINE RELATION S_LFA1_LFB1.LFA1_KTOKK WITH S_T077Y INDEX ACCTGRP_INX
DEFINE RELATION ALL(EKKO_BSART)+ALL(EKKO_BSTYP) WITH S_T161T INDEX DOCTYPE_INX
DEFINE RELATION EKKO_STATU WITH S_DD07T_ESTAK INDEX STATUS_INX
DEFINE RELATION EKPO_MATKL WITH S_T023T INDEX MATGRP_INX
DEFINE RELATION EKKO_ZTERM WITH S_T052U INDEX PAYTERM_INX AS PO_PAYTERM
DEFINE RELATION EKKO_BUKRS WITH S_T001 INDEX COCODE_INX
DEFINE RELATION EKPO_WERKS WITH S_T001W INDEX PLANT_INX
DEFINE RELATION EKPO_PSTYP WITH S_T163Y INDEX ITEMCAT_INX
DEFINE RELATION SUB(ALL(EKKO_ERNAM) 1 10) WITH GAIMS_UserId1 INDEX GAIMS_INX AS CREATOR
DEFINE RELATION SUB(ALL(EKPO_AFNAM) 1 10) WITH GAIMS_UserId1 INDEX GAIMS_INX AS REQUISITIONER
DEFINE RELATION EKPO_MATKL WITH S_DIRECT_INDIRECT INDEX INDEX_DIRECT_INDIRECT

DELETE FIELD GY_DELETION_ID OK
DEFINE FIELD GY_DELETION_ID COMPUTED

"L - Deletion"              IF EKPO_LOEKZ = "L"
"S - Blocked"               IF EKPO_LOEKZ = "S"
""

DELETE FIELD GY_MAT_GRP OK
DEFINE FIELD GY_MAT_GRP COMPUTED

EXCLUDE(ALLTRIM(EKPO_MATKL)+" - "+ALL(S_T023T.T023T_WGBEZ)+" / "+ALL(S_T023T.T023T_WGBEZ60),"~") IF ALL(S_T023T.T023T_WGBEZ60) <> ""
EXCLUDE(ALLTRIM(EKPO_MATKL)+" - "+ALL(S_T023T.T023T_WGBEZ),"~")

COM Define over5k indicator
DELETE FIELD Over5K_Ind OK
DEFINE FIELD Over5K_Ind COMPUTED 

"Over $5K" IF PO_USD_Summ >= 5000
""

COM *****************************************************
COM Extract final tables
COM *****************************************************

EXTRACT FIELDS v_Region AS "Region" EXCLUDE(EKKO_EBELN,"~") AS "Purchasing Doc" EXCLUDE(EKPO_EBELP,"~") AS "Item" EXCLUDE(EKKO_BUKRS+" - "+S_T001.T001_BUTXT,"~") AS "Company_Code" EXCLUDE(ALL(EKPO_WERKS) + " - " + ALL(S_T001W.T001W_NAME1),"~") AS "Plant" EXCLUDE(ALL(EKKO_BSART)+" - "+S_T161T.T161T_BATXT,"~") AS "Document Type" EXCLUDE(EKKO_LIFNR,"~") AS "Vendor" EXCLUDE(LFA1_NAME1,"~") AS "Name1" EXCLUDE(LFA1_NAME3,"~") AS "Name3" EXCLUDE(LFA1_KTOKK+" - "+S_T077Y.T077Y_TXT30,"~") AS "VMD Acct Grp" GY_DELETION_ID AS "Deletion Id" EKPO_MENGE AS "Qty" EXCLUDE(EKPO_BPRME,"~") AS "Unit" EKPO_NETPR AS "Net Price" EXCLUDE(EKKO_WAERS,"~") AS "Ccy" EKPO_NETWR AS "Net Order" Converted_Amount Over5K_Ind PO_USD_Summ Converted_Amount_PODate EXCLUDE(EKKO_ERNAM,"~") AS "PO Creator ID" PROPER(EXCLUDE(ALL(CREATOR.LAST_NAME) + ", " + ALL(CREATOR.FIRST_NAME),"~")) AS "PO Creator Name" EXCLUDE(EKKO_STATU+" - "+S_DD07T_ESTAK.DD07T_DDTEXT,"~") AS "Status" EKKO_BEDAT AS "Document Date" EKKO_AEDAT AS "Created Dt" EXCLUDE(ALL(S_T163Y.T163Y_EPSTP) + " - " + ALL(S_T163Y.T163Y_PTEXT),"~") AS "Item Category" EXCLUDE(EKPO_MATNR,"~") AS "Material" EXCLUDE(EKPO_TXZ01,"~") AS "Short Text" GY_MAT_GRP AS "Mat Grp" EXCLUDE(S_DIRECT_INDIRECT.Direct_Indirect,"~") AS "Direct Indirect" EXCLUDE(EKKO_ZTERM+" - "+PO_PAYTERM.T052U_TEXT1,"~") AS "PO Payment Term" EXCLUDE(ALL(LFB1_ZTERM) + " - " + ALL(VMD_PAYTERM.T052U_TEXT1),"~") AS "VMD Payment Term" TO R1_PODetails_%v_Region%_%v_Batch% IF S_DIRECT_INDIRECT.Direct_Indirect = "Indirect"

DELETE FORMAT T_PO_4 OK

COM *****************************************************
COM Delete temp tables
COM *****************************************************

SET DELETE_FILE ON
DELETE FORMAT T_SORT OK
DELETE FORMAT T_SUMM OK
DELETE FORMAT T_SUMM_1 OK

COM Delete temp index files
EXECUTE 'cmd /c del "*.INX"'

CLOSE PRIMARY SECONDARY
