COM ***********************************************************
COM  SCRIPT NAME:  A02_List_of_POs
COM  SCRIPT DESC:  Run script to extract list of POs
COM  CREATED DATE: 2/15/2021
COM  CREATED BY:   Marion Pacuri
COM  CALLED BY:    A00_Master    
COM  FILES IN:     R1_PODetails*.FIL
COM  FILES OUT:    R1_PODetails.FIL
COM                R2_POSummary.FIL
COM ***********************************************************

SAVE TABLELIST TABLE TO T_Table_List

OPEN T_Table_List
EXTRACT ALL FIELDS TO T_PO_Append IF FIND("R1_PODetails",table_name) OPEN

COM Initialize variables for the GROUP Command
OPEN T_PO_Append
DELETE FIELD Key OK
DEFINE FIELD Key COMPUTED "1"

ASSIGN v_KEY = Key
ASSIGN v_FIELD = BLANKS(500)

DELETE FORMAT T_Flatten OK

COM Run GROUP command to concatenate tables to append
GROUP IF v_KEY = Key
    ASSIGN v_FIELD = SUBSTR(ALLTRIM(ALLTRIM(v_FIELD) + ', ' + ALLTRIM(table_name)),1,500)
    ELSE 
    EXTRACT FIELDS ALL(SUBSTR(ALLTRIM(v_FIELD),2,250)) AS 'table_append' TO T_Flatten EOF
    ASSIGN v_KEY = Key
    ASSIGN v_FIELD = ', ' + ALLTRIM(table_name) 
END

OPEN T_Flatten
LOCATE RECORD 1
v_append = table_append

COM**************************************************************

COM Append all tables into 1 if there are multiple tables
v_COMMENT = "COMMENT"
IF OCCURS(v_append,",") >= 1 v_COMMENT = ""

%v_COMMENT%
APPEND %v_append% TO R1_PODetails

COM**************************************************************

COM Extract the one table if there is only 1
v_COMMENT = "COMMENT"
IF OCCURS(v_append,",") = 0 v_COMMENT = ""

%v_COMMENT%
OPEN %v_append%
EXTRACT ALL FIELDS TO R1_PODetails

COM**************************************************************

OPEN R1_PODetails

DELETE FIELD Category OK
DEFINE FIELD Category COMPUTED

"SRM" IF FIND("SRM",Document_Type)
"SAP"

COM Summarize by cocd and plant
SUMMARIZE ON Region Plant Company_Code Category SUBTOTAL Converted_Amount TO T_SUMM PRESORT OPEN
CROSSTAB ON Region Plant Company_Code COLUMNS Category SUBTOTAL Converted_Amount TO T_CROSSTAB OPEN
EXTRACT ALL FIELDS Converted_Amount_SAP + Converted_Amount_SRM AS "Total_PO_USD" TO T_EXTRACT OPEN
SORT ON Total_PO_USD D TO R2_POSummary OPEN

COM Delete temp tables
DELETE FORMAT T_Table_List OK
DELETE FORMAT T_Flatten OK
DELETE FORMAT T_PO_Append OK
DELETE FORMAT T_SUMM OK
DELETE FORMAT T_CROSSTAB OK
